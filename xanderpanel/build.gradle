//plugins {
//    id "com.github.dcendents.android-maven" version "1.5"
//}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 24
    buildToolsVersion '25.0.2'
    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
//    compile 'com.android.support:appcompat-v7:25.2.0'
//    compile 'com.android.support:support-compat:25.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.inkapplications.viewpageindicator:library:2.4.3'
}

// This is the library version used when deploying the artifact
def siteUrl = 'https://github.com/XanderWang/XanderPanel'  // 项目的主页
def gitUrl = 'git@github.com:XanderWang/XanderPanel.git'   // Git仓库的url
group = "com.xander.panel"                                // Maven Group ID for the artifact，一般填你唯一的包名
version = "1.3.1"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'Android XanderPanel' 	//项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'xandy'		//填写的一些基本信息
                        name 'Xander.Wang'
                        email '420640763@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath = files(android.bootClasspath)
//    classpath += project.files(configurations.compile.files,android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    options.encoding = "utf-8"
    options.charSet = "utf-8"
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "XanderPanel"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}
